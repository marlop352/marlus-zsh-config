() {
	emulate -L zsh

	source /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh

	# Determine terminal capabilities.
	{
		if ! zmodload zsh/langinfo zsh/terminfo || [[ $langinfo[CODESET] != (utf|UTF)(-|)8 || $TERM == (dumb|linux) ]] || (( terminfo[colors] < 256 )); then
			# Don't use the powerline config. It won't work on this terminal.
			local USE_POWERLINE=false
			# Define alias `x` if our parent process is `login`.
			local parent
			if { parent=$(</proc/$PPID/comm) } && [[ ${parent:t} == login ]]; then
				alias x='startx ~/.xinitrc'
			fi
		fi
	} 2>/dev/null

	## Select the theme file
	if [[ $USE_POWERLINE == false ]]; then
		# Use 8 colors and ASCII.
		## Config the theme file
		_POSH_THEME="/usr/share/oh-my-posh/themes/probua.minimal.omp.json"
	else
		# Use 256 colors and UNICODE.
		## Config the theme file from arg. or use fallback theme file
		if [[ "${PATH_OF_THE_THEME}" != "" ]]; then
			_POSH_THEME="${PATH_OF_THE_THEME}"
		else
			_POSH_THEME="/usr/share/oh-my-posh/themes/slimfat.omp.json"
		fi
	fi

	## Apply the theme file
	if [[ -r "${_POSH_THEME}" ]]; then
		export POSH_THEME=${_POSH_THEME}
		eval "$(oh-my-posh init zsh)"
	else
		echo "Warning: \"${_POSH_THEME}\" is unreachable!"
		echo "Oh My Posh theme is disabled! Using the fallback \"elite2\" theme now!"
		echo "Please check your theme file's existance and read permission!"
		echo "Tips: run \"sudo chmod +r -R /usr/share/oh-my-posh/\" may helps when there're permission issues."
		autoload -U promptinit
		promptinit
		prompt elite2 green yellow
	fi
}
